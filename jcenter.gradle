// plugins {
//     id "com.github.dcendents.android-maven" version "1.4.1"  // https://github.com/dcendents/android-maven-gradle-plugin
//     id "com.jfrog.bintray" version "1.7.3"                   // https://github.com/bintray/gradle-bintray-plugin
// }


def configFile = rootProject.file("jcenter.properties")
if (!configFile.exists()) {
    throw new IllegalArgumentException("Can not find '${configFile}'")
}
def config = new Properties()
config.load(configFile.newDataInputStream())

// Maven Information
def __MAVEN_USER = config.getProperty('MAVEN_USER', System.getenv('MAVEN_USER'))
def __MAVEN_KEY = config.getProperty('MAVEN_KEY', System.getenv('MAVEN_KEY'))
def __MAVEN_ORGAN = config.getProperty('MAVEN_ORGAN', System.getenv('MAVEN_ORGAN'))
def __MAVEN_REPO = config.getProperty('MAVEN_REPO', System.getenv('MAVEN_REPO'))
def __LICENSES = config.getProperty('LICENSES', System.getenv('LICENSES'))
// Artifact Information
def __ARTIFACT_GROUP = config.getProperty('ARTIFACT_GROUP', System.getenv('ARTIFACT_GROUP'))
def __ARTIFACT_NAME = config.getProperty('ARTIFACT_NAME', System.getenv('ARTIFACT_NAME'))
def __ARTIFACT_OVERRIDE = Boolean.valueOf(config.getProperty('ARTIFACT_OVERRIDE', System.getenv('ARTIFACT_OVERRIDE')))
// User Information
def __MY_ID = config.getProperty('MY_ID', System.getenv('MY_ID'))
def __MY_NAME = config.getProperty('MY_NAME', System.getenv('MY_NAME'))
def __MY_EMAIL = config.getProperty('MY_EMAIL', System.getenv('MY_EMAIL'))
// Project Information
def __HOME_URL = config.getProperty('HOME_URL', System.getenv('HOME_URL'))
def __VCS_URL = config.getProperty('VCS_URL', System.getenv('VCS_URL'))


apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

    failOnError false
}
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api"
        title __ARTIFACT_NAME
    }
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

group = __ARTIFACT_GROUP
version = android.defaultConfig.versionName
archivesBaseName = __ARTIFACT_NAME
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                url __VCS_URL
                developers {
                    developer {
                        id __MY_ID
                        name __MY_NAME
                        email __MY_EMAIL
                    }
                }
            }
        }
    }
}
bintray {
    user = __MAVEN_USER
    key = __MAVEN_KEY

    configurations = ['archives']
    publish = true
    override = __ARTIFACT_OVERRIDE

    pkg {
        userOrg = __MAVEN_ORGAN
        repo = __MAVEN_REPO
        name = __ARTIFACT_NAME
        websiteUrl = __HOME_URL
        vcsUrl = __VCS_URL
        licenses = [__LICENSES]
    }
}
